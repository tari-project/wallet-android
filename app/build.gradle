import java.nio.file.Files
import java.nio.file.Paths

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

final File secretPropertiesFile = project.rootProject.file("secret.properties")
if (!secretPropertiesFile.exists()) {
    Files.copy(
            Paths.get(project.rootProject.file("secret-example.properties").absolutePath),
            Paths.get(secretPropertiesFile.absolutePath),
    )
}
Properties properties = new Properties()
properties.load(project.rootProject.file("secret.properties").newDataInputStream())
def sentryPublicDSN = properties.getProperty('sentry.public_dsn')

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.tari.android.wallet"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 111
        versionName "v0.1.11-jniLibs-0.9.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters "arm64-v8a", "x86_64", "armeabi-v7a" // fetch missing binaries from  https://tari.com/downloads
        }
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_static'
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            jniDebuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    viewBinding {
        enabled = true
    }

    defaultConfig {
        manifestPlaceholders = [sentryPublicDSN:sentryPublicDSN]
    }

}

dependencies {
    def lifecycle_version = "2.2.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    // android
    implementation 'androidx.appcompat:appcompat:1.1.0'
    // kotlin extensions
    implementation 'androidx.core:core-ktx:1.3.0-rc01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    // android jetpack
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // recycler view
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    // swipe refresh
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"

    // biometric
    implementation 'androidx.biometric:biometric:1.0.1'

    // support lib
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'

    // test
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.mockito:mockito-core:2.19.0'
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // spring animation
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
    implementation 'com.daasuu:EasingInterpolator:1.3.0'

    // joda
    implementation "net.danlew:android.joda:2.10.3"

    // parceler
    implementation 'org.parceler:parceler-api:1.1.13'
    kapt 'org.parceler:parceler:1.1.13'

    // dagger - DI
    implementation 'com.google.dagger:dagger:2.27'
    kapt 'com.google.dagger:dagger-compiler:2.27'

    // rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'

    // unicode library - for emoji processing
    implementation 'com.ibm.icu:icu4j:66.1'

    // lottie
    implementation 'com.airbnb.android:lottie:3.4.0'
    // logger
    implementation 'com.orhanobut:logger:2.2.0'
    // QR scanner
    implementation 'com.budiyev.android:code-scanner:2.1.0'
    implementation 'com.journeyapps:zxing-android-embedded:4.1.0'
    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.8.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.1'

    // encryption
    implementation "de.adorsys.android:securestoragelibrary:1.2.4"

    // matomo - tracking
    implementation 'org.matomo.sdk:tracker:4.1.2'

    // Tor control
    implementation 'net.freehaven.tor.control:jtorctl:0.2'

    // used to read log files
    //noinspection GradleDependency
    implementation 'commons-io:commons-io:2.6'

    // the new view pager
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    // for tab layout
    implementation 'com.google.android.material:material:1.1.0'
    // overscroll
    implementation 'me.everything:overscroll-decor-android:1.0.4'

    // seismic - device shake detector
    implementation 'com.squareup:seismic:1.0.2'

    // sentry - crash analytics
    implementation 'io.sentry:sentry-android:2.0.0'
}
