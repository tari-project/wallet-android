import java.nio.file.Files
import java.nio.file.Paths

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: "../download-jni-binaries.gradle"
// PRIVATE BUILD: comment (add two slashes at the start) the following line for private release
// builds support
apply plugin: 'io.sentry.android.gradle'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.tari.android.wallet"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode buildNumber
        versionName versionNumber + "-jniLibs-" + jniLibsVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters = []
            abiFilters.addAll(supportedABIs)
        }
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_static'
            }
        }
    }

    buildTypes {
        copySentryProps()
        def properties = loadSecretProps()
        debug {
            jniDebuggable true
            buildConfigField("String", "GIPHY_KEY", "\"${properties.getProperty('giphy.key')}\"")
        }
        release {
            buildConfigField("String", "GIPHY_KEY", "\"${properties.getProperty('giphy.key')}\"")
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
//            PRIVATE BUILD: uncomment (remove two slashes at the start) the following line for
//            private release builds support
//            signingConfig signingConfigs.debug

//            PRIVATE BUILD: comment (add two slashes at the start of each line) all the `sentry`
//            block for private release builds support
            sentry { // https://docs.sentry.io/platforms/android/#gradle-configuration
                autoProguardConfig true
                autoUpload true
                uploadNativeSymbols true
                includeNativeSources true
            }
        }
    }

    flavorDimensions "privacy-mode"

    productFlavors {
        regular {
            dimension "privacy-mode"
            buildConfigField "String", "NOTIFICATIONS_API_KEY",
                    "\"${loadSecretProps().getProperty('service.notifications.api_key')}\""
            proguardFile 'regular-proguard-rules.pro'
        }
        privacy {
            dimension "privacy-mode"
        }
    }

    applicationVariants.all { variant ->
        if (variant.productFlavors.get(0).name == "regular") {
            if (variant.buildType.name == "debug") {
                variant.mergedFlavor.manifestPlaceholders.sentryPublicDSN = ''
            } else {
                variant.mergedFlavor.manifestPlaceholders.sentryPublicDSN =
                        loadSecretProps().getProperty('sentry.public_dsn')
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    viewBinding {
        enabled = true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }

}

Properties loadSecretProps() {
    final File secretProps = project.rootProject.file("secret.properties")
    if (!secretProps.exists()) {
        Files.copy(
                Paths.get(project.rootProject.file("secret-example.properties").absolutePath),
                Paths.get(secretProps.absolutePath),
        )
    }
    final Properties properties = new Properties()
    properties.load(secretProps.newDataInputStream())
    return properties
}

void copySentryProps() {
    final File sentryProps = project.rootProject.file("sentry.properties")
    if (!sentryProps.exists()) {
        Files.copy(
                Paths.get(project.rootProject.file("sentry-example.properties").absolutePath),
                Paths.get(sentryProps.absolutePath),
        )
    }
}

preBuild.dependsOn("downloadJNIBinaries")

repositories {
    maven {
        url "http://giphy.bintray.com/giphy-sdk"
    }
}

dependencies {
    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    // coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.5"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"
    // kotlin extensions
    implementation 'androidx.core:core-ktx:1.3.1'
    // android
    implementation 'androidx.appcompat:appcompat:1.1.0'
    // support lib
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    // android jetpack
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    // recycler view
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    // the new view pager
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    // for tab layout
    implementation 'com.google.android.material:material:1.1.0'
    // flex layout
    implementation 'com.google.android:flexbox:2.0.1'
    // overscroll
    implementation 'me.everything:overscroll-decor-android:1.0.4'
    // lottie
    implementation 'com.airbnb.android:lottie:3.4.0'
    // Glide - image processing & caching library
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    // giphy
    implementation 'com.giphy.sdk:ui:1.3.0'
    // spring animation
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
    implementation 'com.daasuu:EasingInterpolator:1.3.0'
    // seismic - device shake detector
    implementation 'com.squareup:seismic:1.0.2'

    // biometric
    implementation 'androidx.biometric:biometric:1.0.1'

    // joda
    implementation "net.danlew:android.joda:2.10.3"

    // parceler
    implementation 'org.parceler:parceler-api:1.1.13'
    kapt 'org.parceler:parceler:1.1.13'

    // dagger - DI
    implementation 'com.google.dagger:dagger:2.27'
    kapt 'com.google.dagger:dagger-compiler:2.27'

    // rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'

    // logger
    implementation 'com.orhanobut:logger:2.2.0'

    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'

    // QR scanner
    implementation 'com.budiyev.android:code-scanner:2.1.0'
    implementation 'com.journeyapps:zxing-android-embedded:4.1.0'

    // encryption
    implementation "de.adorsys.android:securestoragelibrary:1.2.4"

    // Tor control
    implementation 'net.freehaven.tor.control:jtorctl:0.2'

    // used to read log files
    //noinspection GradleDependency
    implementation 'commons-io:commons-io:2.6'

    // JSON reflection-based (+de)serialization library
    implementation 'com.google.code.gson:gson:2.8.6'

    // Retrofit2 - Popular (2020) REST API wrapper with Java interface
    implementation 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.8.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.1'

    // Google services & Google drive
    regularImplementation 'com.google.android.gms:play-services-auth:18.1.0'
    regularImplementation 'com.google.http-client:google-http-client-gson:1.35.0'
    regularImplementation('com.google.api-client:google-api-client-android:1.30.9') {
        exclude group: 'org.apache.httpcomponents'
    }
    regularImplementation('com.google.apis:google-api-services-drive:v3-rev197-1.25.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    // matomo - tracking
    regularImplementation 'org.matomo.sdk:tracker:4.1.2'
    // sentry - crash analytics
    regularImplementation 'io.sentry:sentry-android:2.1.1'

    // test
    testImplementation 'junit:junit:4.13'
    testImplementation "io.mockk:mockk:1.10.0"
    androidTestImplementation "io.mockk:mockk-android:1.10.0"
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
